CREATE OR REPLACE TYPE OBJECT_PL_REPORT IS OBJECT (  
  SEQ_NBR NUMBER,
  TITLE VARCHAR2(128),
  DISPLAY_TXT VARCHAR2(128),
  SUB_TITLE VARCHAR2(128),   
  COA_CDE VARCHAR2(32),   
  AMNT NUMBER,
  TOT_SEQ_NBR_AMNT NUMBER,
  AMNT_PERC NUMBER
);
/
CREATE OR REPLACE TYPE TABLE_PL_REPORT IS TABLE OF OBJECT_PL_REPORT;
/
CREATE OR REPLACE
FUNCTION GET_PL_REPORT(FP_MANAGEMENT_ACCOUNTS_ID IN NUMBER, FP_FROM_DTE IN VARCHAR2, FP_END_DTE IN VARCHAR2, FP_BU_ID IN NUMBER, FP_SITE_ID IN NUMBER, FP_FINYEAR_ID IN NUMBER, FP_COMPANY_ID IN NUMBER) 
RETURN TABLE_PL_REPORT PIPELINED IS
CURSOR CS_PL_REPORT IS   
    SELECT PL.SEQ_NBR, PL.TITLE, PL.DISPLAY_TXT, NVL(PL.SUB_TITLE,PL.COA_TITLE) SUB_TITLE, 
      MAX(CASE WHEN PL.SUB_TITLE IS NULL THEN PL.COA_CDE ELSE NULL END) COA_CDE,
      SUM(PL.AMNT) AMNT, SUM(SUM(PL.AMNT)) OVER(PARTITION BY PL.SEQ_NBR) SEQ_NBR_AMNT
      FROM TABLE(GET_PL(FP_MANAGEMENT_ACCOUNTS_ID, FP_FROM_DTE,FP_END_DTE,FP_BU_ID,FP_SITE_ID,FP_FINYEAR_ID,FP_COMPANY_ID)) PL 
      GROUP BY PL.SEQ_NBR, PL.TITLE, PL.DISPLAY_TXT, NVL(PL.SUB_TITLE,PL.COA_TITLE)
      ORDER BY PL.SEQ_NBR ASC;
  
V_AMNT_PERC NUMBER;    
BEGIN
  FOR FL_PL IN CS_PL_REPORT LOOP
      V_AMNT_PERC := 0;      
      IF (FL_PL.SEQ_NBR_AMNT<>0) THEN
        V_AMNT_PERC := ROUND((FL_PL.AMNT/FL_PL.SEQ_NBR_AMNT)*100,2);
      END IF;
      PIPE ROW (OBJECT_PL_REPORT(FL_PL.SEQ_NBR, FL_PL.TITLE, FL_PL.DISPLAY_TXT, FL_PL.SUB_TITLE, FL_PL.COA_CDE, FL_PL.AMNT, FL_PL.SEQ_NBR_AMNT, V_AMNT_PERC));      
  END LOOP;
END;
/
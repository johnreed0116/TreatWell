CREATE OR REPLACE TYPE OBJECT_SUPPLIER_STATEMENT IS OBJECT (
  COA_CDE VARCHAR2(32),    
  TITLE VARCHAR2(128),    
  VOUCHER_NBR VARCHAR2(16),
  VOUCHER_DTE DATE,  
  BILL_NBR VARCHAR2(32),
  BILL_DTE DATE,  
  AMNT NUMBER,
  GST_AMNT NUMBER,
  PAID_AMNT NUMBER,
  CHEQUE_NBR VARCHAR2(32),
  PAYMENT_MODE VARCHAR2(16)
); 
/
CREATE OR REPLACE TYPE TABLE_SUPPLIER_STATEMENT IS TABLE OF OBJECT_SUPPLIER_STATEMENT;
/
CREATE OR REPLACE
FUNCTION GET_SUPPLIER_STATEMENT(FP_COA_CDE IN VARCHAR2, FP_FROM_DTE IN VARCHAR2, FP_END_DTE IN VARCHAR2, FP_SITE_ID IN NUMBER, FP_COMPANY_ID IN NUMBER) 
RETURN TABLE_SUPPLIER_STATEMENT PIPELINED IS
CURSOR CS_DATA IS 
  SELECT SS.*, COA.TITLE FROM (
      SELECT VD.COA_CDE, VM.VOUCHER_DTE, (VST.VOUCHER_SUB_TYP_DESC||'-'||VM.VOUCHER_NBR) VOUCHER_NBR, VD.BILL_NBR, VD.BILL_DTE, 
           CASE WHEN VST.VOUCHER_SUB_TYP_DESC IN ('PI') THEN ABS(NVL(VD.AMNT,0))
                WHEN VST.VOUCHER_SUB_TYP_DESC IN ('JV') THEN NVL(VD.AMNT,0) ELSE (-1*NVL(VD.AMNT,0)) END AMNT, 
           CASE WHEN VST.VOUCHER_SUB_TYP_DESC IN ('PI') THEN NVL((SELECT SUM(VD_1.AMNT) FROM VOUCHER_DETAIL VD_1
                WHERE VD_1.VOUCHER_MASTER_ID=VM.VOUCHER_MASTER_ID AND VD_1.COA_CDE IN ('300-02-01-0004','100-15-01-0002') AND VD_1.AMNT>0),0)
                WHEN VST.VOUCHER_SUB_TYP_DESC IN ('PR') THEN NVL((SELECT SUM(VD_1.AMNT) FROM VOUCHER_DETAIL VD_1
                WHERE VD_1.VOUCHER_MASTER_ID=VM.VOUCHER_MASTER_ID AND VD_1.COA_CDE IN ('300-02-01-0004','100-15-01-0002') AND VD_1.AMNT<0),0) ELSE 0 END GST_AMNT, 
           0 PAID_AMNT, NULL CHEQUE_NBR, NULL PAYMENT_MODE 
           FROM VOUCHER_DETAIL VD, VOUCHER_MASTER VM, VOUCHER_SUB_TYPE VST
           WHERE VD.VOUCHER_MASTER_ID=VM.VOUCHER_MASTER_ID AND VM.VOUCHER_SUB_TYP_ID=VST.VOUCHER_SUB_TYP_ID
           AND VM.CANCELLED_BY IS NULL AND VM.POSTED_IND='Y' AND (VM.SITE_ID=FP_SITE_ID OR FP_SITE_ID=0)
           AND ((VST.VOUCHER_SUB_TYP_DESC IN ('PI') AND VD.AMNT<0) OR (VST.VOUCHER_SUB_TYP_DESC IN ('PR') AND VD.AMNT>0) 
                OR (VST.VOUCHER_SUB_TYP_DESC IN ('JV') AND VD.AMNT<0)) AND VM.COMPANY_ID=FP_COMPANY_ID           
           AND VM.VOUCHER_DTE BETWEEN TO_DATE(FP_FROM_DTE,'DD-MM-YYYY') AND TO_DATE(FP_END_DTE,'DD-MM-YYYY') 
           AND (VD.COA_CDE=FP_COA_CDE OR FP_COA_CDE='0')
           AND VD.COA_CDE IN (SELECT COA.COA_CDE FROM CHART_OF_ACCOUNT COA WHERE COA.ENTRY_LEVEL_IND='Y' 
              START WITH COA.COA_CDE IN (SELECT CAD.COA_CDE FROM COA_ASSOCIATION_DETAIL CAD, COA_ASSOCIATION CA 
              WHERE CAD.COA_ASSOCIATION_ID=CA.COA_ASSOCIATION_ID AND CA.ASSOCIATION_TITLE='PAYABLE ACCOUONTS') 
              CONNECT BY PRIOR COA.COA_CDE=COA.PARENT_CDE)        
      UNION ALL              
      SELECT VD.COA_CDE, VM.VOUCHER_DTE, (VST.VOUCHER_SUB_TYP_DESC||'-'||VM.VOUCHER_NBR) VOUCHER_NBR, NULL BILL_NBR, NULL BILL_DTE, 0 AMNT, 0 GST_AMNT, VD.AMNT PAID_AMNT, VM.CHEQUE_NBR,
          CASE WHEN VM.CHEQUE_NBR IS NOT NULL AND VM.PAYMENT_MODE IS NULL THEN 'CHQ' WHEN VM.PAYMENT_MODE IS NOT NULL THEN VM.PAYMENT_MODE ELSE NULL END PAYMENT_MODE 
           FROM VOUCHER_DETAIL VD, VOUCHER_MASTER VM, VOUCHER_SUB_TYPE VST
           WHERE VD.VOUCHER_MASTER_ID=VM.VOUCHER_MASTER_ID AND VM.VOUCHER_SUB_TYP_ID=VST.VOUCHER_SUB_TYP_ID
           AND VM.CANCELLED_BY IS NULL AND VM.POSTED_IND='Y' AND (VM.SITE_ID=FP_SITE_ID OR FP_SITE_ID=0)
           AND VST.VOUCHER_SUB_TYP_DESC IN ('BPV','BTV','CPV','JV','SI') AND VD.AMNT>0 AND VM.COMPANY_ID=FP_COMPANY_ID
           AND VM.VOUCHER_DTE BETWEEN TO_DATE(FP_FROM_DTE,'DD-MM-YYYY') AND TO_DATE(FP_END_DTE,'DD-MM-YYYY') 
           AND (VD.COA_CDE=FP_COA_CDE OR FP_COA_CDE='0')
           AND VD.COA_CDE IN (SELECT COA.COA_CDE FROM CHART_OF_ACCOUNT COA WHERE COA.ENTRY_LEVEL_IND='Y' 
              START WITH COA.COA_CDE IN (SELECT CAD.COA_CDE FROM COA_ASSOCIATION_DETAIL CAD, COA_ASSOCIATION CA 
              WHERE CAD.COA_ASSOCIATION_ID=CA.COA_ASSOCIATION_ID AND CA.ASSOCIATION_TITLE='PAYABLE ACCOUONTS') 
              CONNECT BY PRIOR COA.COA_CDE=COA.PARENT_CDE)              
  ) SS, CHART_OF_ACCOUNT COA                 
  WHERE SS.COA_CDE=COA.COA_CDE 
  ORDER BY SS.COA_CDE, SS.VOUCHER_DTE;

BEGIN
    FOR FL_DATA IN CS_DATA LOOP
        PIPE ROW (OBJECT_SUPPLIER_STATEMENT(FL_DATA.COA_CDE, FL_DATA.TITLE, FL_DATA.VOUCHER_NBR, FL_DATA.VOUCHER_DTE, FL_DATA.BILL_NBR, FL_DATA.BILL_DTE,
          FL_DATA.AMNT, FL_DATA.GST_AMNT, FL_DATA.PAID_AMNT, FL_DATA.CHEQUE_NBR, FL_DATA.PAYMENT_MODE));          
    END LOOP;  
  RETURN;  
END;
/
CREATE OR REPLACE TYPE OBJECT_PL_COA_LIST IS OBJECT (
  SEQ_NBR NUMBER,
  TITLE VARCHAR2(128),
  DISPLAY_TXT VARCHAR2(128),
  SUB_TITLE VARCHAR2(128),   
  COA_CDE VARCHAR2(32),
  COA_TITLE VARCHAR2(128)
);
/
CREATE OR REPLACE TYPE TABLE_PL_COA_LIST IS TABLE OF OBJECT_PL_COA_LIST;
/
CREATE OR REPLACE
FUNCTION GET_PL_COA_LIST(FP_MANAGEMENT_ACCOUNTS_ID IN NUMBER) 
RETURN TABLE_PL_COA_LIST PIPELINED IS
CURSOR CS_PL IS 
  SELECT MAM.SEQ_NBR, MAM.TITLE, MAM.DISPLAY_TXT, MAD.SUB_TITLE, MAD.COA_CDE, MAD.EXCL_COA_CDE   
  FROM MANAGEMENT_ACCOUNTS MA, MANAGEMENT_ACCOUNTS_MASTER MAM, MANAGEMENT_ACCOUNTS_DETAIL MAD
    WHERE MA.MANAGEMENT_ACCOUNTS_ID=MAM.MANAGEMENT_ACCOUNTS_ID
      AND MAM.MANAGEMENT_ACCOUNTS_MASTER_ID=MAD.MANAGEMENT_ACCOUNTS_MASTER_ID
      AND MA.MANAGEMENT_ACCOUNTS_ID=FP_MANAGEMENT_ACCOUNTS_ID      
      ORDER BY MAM.SEQ_NBR;

CURSOR CS_COA(CP_COA_CDE IN VARCHAR2,CP_EXCL_COA_CDE IN VARCHAR2) IS  
    SELECT COA.COA_CDE, COA.TITLE FROM CHART_OF_ACCOUNT COA WHERE COA.ENTRY_LEVEL_IND='Y'
      AND COA.COA_CDE NOT IN (SELECT REGEXP_SUBSTR(CP_EXCL_COA_CDE, '[^,]+', 1, LEVEL) VCH_SUB_TYP FROM DUAL 
      CONNECT BY LEVEL <= LENGTH(REGEXP_REPLACE(CP_EXCL_COA_CDE, '[^,]+')) + 1)
      START WITH COA.COA_CDE=CP_COA_CDE CONNECT BY PRIOR COA.COA_CDE=COA.PARENT_CDE;

V_DISPLAY_TXT VARCHAR2(128);       
BEGIN  
  V_DISPLAY_TXT := NULL;
  FOR FL_PL IN CS_PL LOOP    
    IF (V_DISPLAY_TXT IS NULL) OR (V_DISPLAY_TXT <> FL_PL.DISPLAY_TXT) THEN  
      PIPE ROW (OBJECT_PL_COA_LIST(FL_PL.SEQ_NBR, FL_PL.TITLE, FL_PL.DISPLAY_TXT, FL_PL.SUB_TITLE, NULL, NULL));
      V_DISPLAY_TXT := FL_PL.DISPLAY_TXT;
    END IF;
    FOR FL_COA IN CS_COA(FL_PL.COA_CDE,FL_PL.EXCL_COA_CDE) LOOP   
      PIPE ROW (OBJECT_PL_COA_LIST(FL_PL.SEQ_NBR, FL_PL.TITLE, FL_PL.DISPLAY_TXT, FL_PL.SUB_TITLE, FL_COA.COA_CDE, FL_COA.TITLE));        
    END LOOP;              
  END LOOP;   
END;
/
CREATE OR REPLACE TYPE OBJECT_GENERAL_TEMPLATE IS OBJECT (
  FIN_YEAR_ID NUMBER,
  SITE_ID NUMBER,  
  SEQ_NBR NUMBER,
  TITLE VARCHAR2(128), 
  DISPLAY_TXT VARCHAR2(128),
  SUB_TITLE VARCHAR2(128),   
  COA_CDE VARCHAR2(32),
  AMNT NUMBER,
  VOUCHER_DTE DATE,
  GTD_ID NUMBER,
  COMPANY_ID NUMBER
);
/
CREATE OR REPLACE TYPE TABLE_GENERAL_TEMPLATE IS TABLE OF OBJECT_GENERAL_TEMPLATE;
/
CREATE OR REPLACE
FUNCTION GET_GENERAL_TEMPLATE(FP_GENERAL_TEMPLATE_ID IN NUMBER, FP_FROM_DTE IN VARCHAR2, FP_END_DTE IN VARCHAR2, FP_SITE_ID IN NUMBER, FP_FINYEAR_ID IN NUMBER, FP_COMPANY_ID IN NUMBER) 
RETURN TABLE_GENERAL_TEMPLATE PIPELINED IS
CURSOR CS_GENERAL_TEMPLATE IS 
    SELECT GTD.GENERAL_TEMPLATE_DETAIL_ID, GTM.SEQ_NBR, GTM.TITLE, GTM.DISPLAY_TXT, GTD.SUB_TITLE, GTD.COA_CDE,
      GTD.SOURCE_TYP DETAIL_SOURCE_TYP, GTD.SOURCE_TXT, GTM.SOURCE_TYP MASTER_SOURCE_TYP, GTD.VOUCHER_SUB_TYP
      FROM GENERAL_TEMPLATE GT, GENERAL_TEMPLATE_MASTER GTM, GENERAL_TEMPLATE_DETAIL GTD
      WHERE GT.GENERAL_TEMPLATE_ID=GTM.GENERAL_TEMPLATE_ID
      AND GTM.GENERAL_TEMPLATE_MASTER_ID=GTD.GENERAL_TEMPLATE_MASTER_ID(+)
      AND GT.GENERAL_TEMPLATE_ID=FP_GENERAL_TEMPLATE_ID
      ORDER BY GTM.SEQ_NBR;

CURSOR CS_DATA(CP_COA_CDE IN VARCHAR2,CP_FROM_DTE IN VARCHAR2,CP_SOURCE_TYP IN VARCHAR2,CP_SOURCE_TXT IN VARCHAR2,CP_VOUCHER_SUB_TYP IN VARCHAR2) IS  
    SELECT VD.COA_CDE, VM.VOUCHER_DTE,
      SUM(CASE WHEN (CP_SOURCE_TYP='DR' AND CP_SOURCE_TXT='VAL' AND VD.AMNT>0) THEN NVL(VD.AMNT,0) 
               WHEN (CP_SOURCE_TYP='CR' AND CP_SOURCE_TXT='VAL' AND VD.AMNT<0) THEN NVL(VD.AMNT,0)
               WHEN (CP_SOURCE_TYP='BOTH' AND CP_SOURCE_TXT='VAL' ) THEN NVL(VD.AMNT,0)
               WHEN (CP_SOURCE_TYP='DR' AND CP_SOURCE_TXT='QTY' AND VD.QTY>0) THEN NVL(VD.QTY,0) 
               WHEN (CP_SOURCE_TYP='CR' AND CP_SOURCE_TXT='QTY' AND VD.QTY<0) THEN NVL(VD.QTY,0)                
               WHEN (CP_SOURCE_TYP='BOTH' AND CP_SOURCE_TXT='QTY' ) THEN NVL(VD.QTY,0) ELSE 0 END) AMNT
      FROM VOUCHER_DETAIL VD, VOUCHER_MASTER VM, SITE S
      WHERE VM.VOUCHER_MASTER_ID=VD.VOUCHER_MASTER_ID 
       AND VM.SITE_ID=S.SITE_ID AND VM.SITE_ID=FP_SITE_ID 
       AND VM.COMPANY_ID=FP_COMPANY_ID 
       AND VM.CANCELLED_BY IS NULL AND VM.POSTED_IND='Y'
       AND ((CP_SOURCE_TYP<>'BOTH' AND VM.VOUCHER_DTE BETWEEN TO_DATE(CP_FROM_DTE,'DD-MM-YYYY') AND TO_DATE(FP_END_DTE,'DD-MM-YYYY'))
            OR CP_SOURCE_TYP='BOTH' AND VM.VOUCHER_DTE <= TO_DATE(FP_END_DTE,'DD-MM-YYYY'))
       AND VD.COA_CDE IN (SELECT COA.COA_CDE FROM CHART_OF_ACCOUNT COA WHERE COA.ENTRY_LEVEL_IND='Y'
                            START WITH COA.COA_CDE=CP_COA_CDE CONNECT BY PRIOR COA.COA_CDE=COA.PARENT_CDE)
       AND VM.VOUCHER_SUB_TYP_ID IN (
            SELECT VOUCHER_SUB_TYP_ID FROM VOUCHER_SUB_TYPE WHERE CP_VOUCHER_SUB_TYP IS NULL
            UNION ALL
            SELECT TO_NUMBER(REGEXP_SUBSTR(CP_VOUCHER_SUB_TYP, '[^;]+', 1, LEVEL)) VCH_SUB_TYP FROM DUAL
              CONNECT BY LEVEL <= LENGTH(REGEXP_REPLACE(CP_VOUCHER_SUB_TYP, '[^;]+')) + 1)                            
       GROUP BY VD.COA_CDE, VM.VOUCHER_DTE ORDER BY VD.COA_CDE, VM.VOUCHER_DTE;
       
V_BUYSELL_DTE DATE;       
V_FROM_DTE VARCHAR2(16);

BEGIN  
  FOR FL_GT IN CS_GENERAL_TEMPLATE LOOP    
    IF (FL_GT.COA_CDE IS NULL OR FL_GT.MASTER_SOURCE_TYP='FORMULA') THEN
      PIPE ROW (OBJECT_GENERAL_TEMPLATE(FP_FINYEAR_ID, FP_SITE_ID, FL_GT.SEQ_NBR, FL_GT.TITLE, FL_GT.DISPLAY_TXT, NVL(FL_GT.SUB_TITLE,FL_GT.TITLE), NULL, 0, NULL, FL_GT.GENERAL_TEMPLATE_DETAIL_ID, FP_COMPANY_ID));
    ELSIF (FL_GT.SUB_TITLE='FUEL SALES') THEN   
      SELECT S.BUYSELL_START_DTE INTO V_BUYSELL_DTE FROM SITE S WHERE S.SITE_ID=FP_SITE_ID;
      IF (V_BUYSELL_DTE IS NOT NULL AND TO_DATE(FP_FROM_DTE,'DD-MM-YYYY')<V_BUYSELL_DTE) THEN
        V_FROM_DTE := TO_CHAR(V_BUYSELL_DTE,'DD-MM-YYYY');
      ELSE 
        V_FROM_DTE := FP_FROM_DTE;
      END IF;
      FOR FL_DATA IN CS_DATA(FL_GT.COA_CDE,V_FROM_DTE, FL_GT.DETAIL_SOURCE_TYP, FL_GT.SOURCE_TXT,FL_GT.VOUCHER_SUB_TYP) LOOP        
         PIPE ROW (OBJECT_GENERAL_TEMPLATE(FP_FINYEAR_ID, FP_SITE_ID, FL_GT.SEQ_NBR, FL_GT.TITLE, FL_GT.DISPLAY_TXT, FL_GT.SUB_TITLE, FL_DATA.COA_CDE, FL_DATA.AMNT, FL_DATA.VOUCHER_DTE, FL_GT.GENERAL_TEMPLATE_DETAIL_ID, FP_COMPANY_ID));        
      END LOOP;        
   ELSE   
      FOR FL_DATA IN CS_DATA(FL_GT.COA_CDE,FP_FROM_DTE, FL_GT.DETAIL_SOURCE_TYP, FL_GT.SOURCE_TXT,FL_GT.VOUCHER_SUB_TYP) LOOP        
         PIPE ROW (OBJECT_GENERAL_TEMPLATE(FP_FINYEAR_ID, FP_SITE_ID, FL_GT.SEQ_NBR, FL_GT.TITLE, FL_GT.DISPLAY_TXT, FL_GT.SUB_TITLE, FL_DATA.COA_CDE, FL_DATA.AMNT, FL_DATA.VOUCHER_DTE, FL_GT.GENERAL_TEMPLATE_DETAIL_ID, FP_COMPANY_ID));        
      END LOOP;        
   END IF;   
  END LOOP;   
END;
/
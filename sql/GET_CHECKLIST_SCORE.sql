CREATE OR REPLACE TYPE OBJECT_CHECKLIST_SCORE IS OBJECT (
  SECTION_ID NUMBER, 
  SECTION_TITLE VARCHAR2(250), 
  SUBSECTION_ID NUMBER,
  SUBSECTION_TITLE VARCHAR2(500),
  SUBSECTION_SCORE NUMBER, 
  SUBSECTION_REMARKS VARCHAR2(250),
  SUBSECTION_ACTION_IND VARCHAR2(1),
  CHECKLIST_DETAIL_ID NUMBER,
  PREPARED_BY VARCHAR2(32),
  PREPARED_DTE DATE
);
/
CREATE OR REPLACE TYPE TABLE_CHECKLIST_SCORE IS TABLE OF OBJECT_CHECKLIST_SCORE;
/
CREATE OR REPLACE
FUNCTION GET_CHECKLIST_SCORE(FP_SITE_ID IN NUMBER, FP_MONTH IN VARCHAR2) 
RETURN TABLE_CHECKLIST_SCORE PIPELINED IS
CURSOR CS_CHECKLIST_SCORE IS 
    SELECT SEC.SECTION_ID,SEC.TITLE SECTION_TITLE, 
       SB.SUBSECTION_ID,SB.TITLE SUBSECTION_TITLE,CD.SCORE SUBSECTION_SCORE, 
       CD.REMARKS SUBSECTION_REMARKS,CD.ACTION_IND SUBSECTION_ACTION_IND, 
       CD.CHECKLIST_DETAIL_ID,CM.PREPARED_BY, CM.PREPARED_DTE
       FROM SECTION SEC,SUBSECTION SB,CHECKLIST_DETAIL CD,CHECKLIST_MASTER CM 
       WHERE SB.SUBSECTION_ID=CD.SUBSECTION_ID 
       AND CD.CHECKLIST_MASTER_ID=CM.CHECKLIST_MASTER_ID 
       AND SB.SECTION_ID=SEC.SECTION_ID 
       AND (CM.SITE_ID,CM.SECTION_ID,CM.REVIEW_DTE) IN ( 
       SELECT CM_1.SITE_ID,CM_1.SECTION_ID,MAX(CM_1.REVIEW_DTE) REVIEW_DTE FROM CHECKLIST_MASTER CM_1 
        WHERE TO_CHAR(CM_1.REVIEW_DTE,'MON-YYYY')=UPPER(FP_MONTH) AND CM_1.SITE_ID=FP_SITE_ID  
        GROUP BY CM_1.SITE_ID,CM_1.SECTION_ID ) 
       AND TO_CHAR(CM.REVIEW_DTE,'MON-YYYY')=UPPER(FP_MONTH)
       AND CM.SITE_ID=FP_SITE_ID   
       ORDER BY SB.SECTION_ID,SB.SUBSECTION_ID;
       
BEGIN
  FOR FL_CLS IN CS_CHECKLIST_SCORE LOOP
    PIPE ROW (OBJECT_CHECKLIST_SCORE(FL_CLS.SECTION_ID, FL_CLS.SECTION_TITLE, FL_CLS.SUBSECTION_ID, FL_CLS.SUBSECTION_TITLE,
                                      FL_CLS.SUBSECTION_SCORE, FL_CLS.SUBSECTION_REMARKS, FL_CLS.SUBSECTION_ACTION_IND, 
                                      FL_CLS.CHECKLIST_DETAIL_ID, FL_CLS.PREPARED_BY, FL_CLS.PREPARED_DTE));
  END LOOP;  
  RETURN;  
END;
/